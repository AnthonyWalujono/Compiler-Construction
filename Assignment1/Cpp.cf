PDefs.   Program ::= [Def] ;

DFun.    Def    ::= Type Id "(" [Arg] ")" "{" [Stm] "}" ;

DUsing.  Def    ::= "using" QConst ";" ;
DDecl. Def ::= Type [Id] ;
DTypedef. Def ::= "typedef" Type Id ";" ; 

terminator Def "" ;

ADecl.   Arg    ::= Type Id ;

separator Arg "," ;

SExp.        Stm ::= Exp ";" ;
SDecls.      Stm ::= Type [Id] ";" ;
SInit.       Stm ::= Type Id "=" Exp ";" ;
SReturn.     Stm ::= "return" Exp ";" ;
SReturnVoid. Stm ::= "return" ";" ;
SWhile.      Stm ::= "while" "(" Exp ")" Stm ;
SBlock.      Stm ::= "{" [Stm] "}" ;
SIfElse.     Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
SIf.         Stm ::= "if" "(" Exp ")" Stm ;
STypeDef.    Stm ::= "typedef" Type Id ";" ;

terminator Stm "" ;

ELit.    Exp16 ::= [String] ;

EQConst. Exp15 ::= QConst ;
QConstName. QConst ::= Name ;
QConstList. QConst ::= QConst "::" Name ;
EIdx.  Exp15 ::= Id "[" Exp "]" ; 
NameTempId. Name ::= Id "<" Type ">" ; 
NameId. Name ::= Id ; 

ETrue.   Exp15  ::= "true" ;
EFalse.  Exp15  ::= "false" ;
EInt.    Exp15  ::= Integer ;
EDouble. Exp15  ::= Double ;
EString. Exp15  ::= String ;
EId.     Exp15  ::= Id ; 

EApp.    Exp15  ::= Id "(" [Exp] ")" ;

EProj.   Exp14  ::= Exp15 "." Exp14 ;
EPIncr.  Exp14  ::= Exp15 "++" ;
EPDecr.  Exp14  ::= Exp15 "--" ;

EIncr.   Exp13  ::= "++" Exp14 ;
EDecr.   Exp13  ::= "--" Exp14 ;

ETimes.  Exp12  ::= Exp12 "*"  Exp13 ;
EDiv.    Exp12  ::= Exp12 "/"  Exp13 ;
EMod.    Exp12  ::= Exp12 "%"  Exp13 ;
EPlus.   Exp11  ::= Exp11 "+"  Exp12 ;
EMinus.  Exp11  ::= Exp11 "-"  Exp12 ;
ERight.  Exp10  ::= Exp10 ">>" Exp11 ;
ELeft.   Exp10  ::= Exp10 "<<" Exp11 ;
ELt.     Exp9   ::= Exp9  "<"  Exp10 ;
EGt.     Exp9   ::= Exp9  ">"  Exp10 ;
ELtEq.   Exp9   ::= Exp9  "<=" Exp10 ;
EGtEq.   Exp9   ::= Exp9  ">=" Exp10 ;
EEq.     Exp8   ::= Exp8  "==" Exp9 ;
ENEq.    Exp8   ::= Exp8  "!=" Exp9 ;
EAnd.    Exp4   ::= Exp4  "&&" Exp5 ;
EOr.     Exp3   ::= Exp3  "||" Exp4 ;
EAss.    Exp2   ::= Exp3 "=" Exp2 ;
ETer.    Exp2   ::= Exp2 "?" Exp2 ":" Exp2 ;
EThrow.  Exp1   ::= "throw" Id "(" Exp ")" ; 

internal ETyped. Exp15 ::= "(" Exp ":" Type ")" ;

coercions Exp 16 ;

TypeQConst. Type ::= QConst ; 
TypePreMod. Type ::= PreModifier Type ; 
TypePostMod. Type ::= Type PostModifier ; 

separator Exp "," ;
separator nonempty String ""; 

rules Type   ::= "bool" | "int" | "double" | "void" ;
rules PreModifier ::= "const" ; 
rules PostModifier ::= "&" | "*" ; 

token Id (letter (letter | digit | '_')*) ;

separator nonempty Id "," ;

comment "#" ;
comment "//" ;
comment "/*" "*/" ;
